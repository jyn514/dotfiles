#!/usr/bin/env zsh
# docs: https://zsh.sourceforge.io/Doc/Release/index.html#Top
# to see the keymap, `bindkey` (-M for a specific keymap)
# to see all completions, `echo $_comps`. for a specific completion, `which _ls` *usually* works.
# to debug a specific completion, `C-x ? Up`
# to see all functions, `print -l ${(ok)functions}`
# to profile startup, run `zsh -i -c zprof` (this is more accurate than putting zprof at the end of zshrc)
# to see an even more detailed profile, use `time zsh -ic exit | ts %.S`
# running time on functions: `time (foo)`
# (unfortunately this can't be fixed automatically because `time` takes a whole pipeline as an argument, not a normal list of shell words)

ZSH_PROFILE_READ=1

# zmodload zsh/zprof

alias help=run-help
alias -g stauts=status
alias -g serach=search
alias -g searhc=search

if [ -z $DOTFILES ]; then
  # Set "$0" to zsh, not .profile
  emulate sh
  # remove conflicting aliases
  unalias ls la ll l alert ip .. 2>/dev/null
  # Set up the prompt
  . ~/.profile
  emulate zsh
fi


ABBR_AUTOLOAD=0
RPS1="%F{240}loading%f"

# package manager
source $ZDOTDIR/antidote/antidote.zsh
antidote load

zsh-defer -12p bindkey '^[l' autosuggest-execute
zsh-defer -12p abbr import-aliases --session --quiet
zsh-defer -2p abbr erase cat  # really annoying when running sudo commands

# space expands !... history and abbreviations
# TODO: see if we can make this more useful?
extra-magic-space() {
	abbr-expand-and-insert
	zle expand-history || true
}
zle -N extra-magic-space
bindkey ' ' magic-space
zsh-defer -12p bindkey ' ' extra-magic-space

pure_shell() {
   env -i HOME="$HOME" TERM="$TERM" PS1='; ' DISPLAY="$DISPLAY" zsh --no-rcs --no-globalrcs "$@"
}

autoload -Uz promptinit
promptinit
PROMPT="$PS1"
POSTEDIT="$PS0"
# Perform command substitution when evaluating prompt
setopt prompt_subst

setopt histignorealldups sharehistory correct
# append immediately, not on close
setopt incappendhistory
# allow `cd ..`
setopt autocd
# store all directories into the directory stack automatically
setopt autopushd
setopt interactive_comments

# save command in buffer before executing
bindkey "^Q" push-line
zle-vicmd-push() { zle push-line; zle vi-insert; }
zle-vicmd-copy() {
    zle vi-yank
    echo "$CUTBUFFER" | xclip -i -selection clipboard
}
zle-sudo() {
  zle up-line-or-history
  LBUFFER="sudo $LBUFFER"
}
zle -N zle-vicmd-push
zle -N zle-vicmd-copy
zle -N zle-sudo
bindkey -a "^Q" zle-vicmd-push
bindkey -a "y" zle-vicmd-copy
bindkey '^[k' zle-sudo
bindkey "^G" get-line
bindkey -a "^G" get-line
bindkey '^@' expand-word
bindkey -a '@' expand-word

autoload edit-command-line
zle -N edit-command-line
bindkey -a K edit-command-line
bindkey '^X^E' edit-command-line

# autoescape pasted urls
autoload -U url-quote-magic bracketed-paste-magic
zle -N self-insert url-quote-magic
zle -N bracketed-paste bracketed-paste-magic

# use emacs line editing for insert mode, ...
bindkey -e
# ... but escape still switches into vi command mode
bindkey "^[" vi-cmd-mode
# the default timeout is *just* short enough i sometimes hit esc-b fast enough ZLE counts it as a single command
KEYTIMEOUT=10
# show a line instead of a block in insert mode
# https://unix.stackexchange.com/a/765611/229104
__insert_mode() { printf "\e[5 q"; }
__command_mode() { printf "\e[2 q"; }
zle-keymap-select () {
    if [[ $KEYMAP == vicmd ]]; then
        __command_mode
    else
        # the insert mode for vi
        __insert_mode
    fi
}
zle-line-init() { __insert_mode; }
zle-line-finish() { __command_mode; }
# register our new widget
# https://zsh.sourceforge.io/Doc/Release/Zsh-Line-Editor.html#Special-Widgets
zle -N zle-keymap-select
zle -N zle-line-init
zle -N zle-line-finish

# Alt + Right
bindkey ";3C" forward-word
# kitty passes \e[ but gnome-terminal doesn't :(
bindkey "\e[1;3C" forward-word
# Alt + Left
bindkey ";3D" backward-word
bindkey "\e[1;3D" backward-word
# ctrl+left
bindkey ";5D" backward-word
bindkey "\e[1;5D" backward-word
# ctrl+right
bindkey ";5C" forward-word
bindkey "\e[1;5C" forward-word

# by analogy with ^H
# this overrides clear-screen, but i don't care about clear-screen
bindkey "^L" delete-word
# these are backwards because i have them bound to vi keys
bindkey "${termcap[kN]}" backward-word
bindkey "${termcap[kP]}" forward-word

# properly bind Home, End, Delete (had trouble with this at one point)
# note: MUST come after 'bindkey -e'
bindkey "\e[H" beginning-of-line
bindkey "\e[F" end-of-line
bindkey "\e[3~" delete-char
# tmux gets these as different scancodes apparently?
bindkey "\e[1~" beginning-of-line
bindkey "\e[4~" end-of-line
# ctrl+del
bindkey "5~" delete-word
bindkey "\e[3;5~" delete-word # tmux
# alt+del
bindkey "3~" delete-word
# ctrl+backspace
bindkey '^H' backward-delete-word

# shift-tab cycles backwards through choices
bindkey '^[[Z' reverse-menu-complete

# autoload -Uz surround
# zle -N delete-surround surround
# zle -N add-surround surround
# zle -N change-surround surround
# bindkey -a mc change-surround
# bindkey -a md delete-surround
# bindkey -a my add-surround
# bindkey -M visual S add-surround
autoload -U select-bracketed
zle -N select-bracketed
for m in visual viopp; do
  for c in {a,i}${(s..)^:-'()[]{}<>bB'}; do
    bindkey -M $m $c select-bracketed
  done
done

SAVEHIST=10000000
HISTSIZE=$SAVEHIST
HISTFILE=~/.zsh_history

zstyle ':completion:*' auto-description 'specify: %d'
zstyle ':completion:*' completer _expand _complete _correct _approximate
zstyle ':completion:*' format 'Completing %d'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' menu select=2
if exists dircolors; then
  eval "$(dircolors -b)"
fi
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' list-colors ''
zstyle ':completion:*' list-prompt %SAt %p: Hit TAB for more, or the character to insert%s
# fuzzy-matching, like fzf but it works even there is a partial prompt that's being completed
zstyle ':completion:*' matcher-list '' 'm:{a-z}={A-Z}' 'm:{a-zA-Z}={A-Za-z}' 'r:|[._-]=* r:|=* l:|=* m:{a-zA-Z}={A-Za-z}'
zstyle ':completion:*' menu select=long
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s
zstyle ':completion:*' use-compctl false
zstyle ':completion:*' verbose true

zstyle ':completion:*:*:kill:*:processes' list-colors '=(#b) #([0-9]#)*=0=01;31'
# TODO: these both break on macOS
zstyle ':completion:*:kill:*' command 'ps -u $USER -o pid,%cpu,tty,cputime,cmd'
zstyle ':completion:*:gdb:*' command 'ps --ppid 2 --pid 2 --deselect -o pid,tty,cmd'
zstyle ':completion:*' rehash true

if ! exists __builtin_gdb_complete; then
  autoload +X _gdb
  functions -c _gdb __builtin_gdb_complete
fi
# builtin completion is broken, lol, lmao
_gdb() {
  if [ "$words[CURRENT-1]" = '-p' ]; then
    _pids
  else
  __builtin_gdb_complete
 fi
}

# fzf bs
zstyle ':fzf-tab:complete:*' fzf-bindings 'right:accept'
zstyle ':fzf-tab:*' continuous-trigger 'tab'
zstyle ':fzf-tab:*' accept-line enter
# use / instead of tab to immediately trigger another completion, like `right`
#zstyle ':fzf-tab:*cd*' continuous-trigger '/'

# making this part of normal tab complete is too slow and hangs
# really wish zsh would show partial matches instead of waiting for all of them to finish
fzf-file() {
  local selected
  IFS=$'\n' selected=($(fd | fzf --expect=tab --no-sort --query="$BUFFER"))
  if [[ "$selected" ]]; then
    LBUFFER="$selected"
    if [[ ${#selected[@]} -eq 2 ]]; then
      LBUFFER="${selected[2]}"
      zle reset-prompt
    fi
  fi
  zle accept-line
}
zle -N fzf-file
bindkey "^[t" fzf-file

zsh-defer -12p -c "$(atuin init zsh --disable-up-arrow)"
zsh-defer -12p bindkey -M vicmd '^r' atuin-search
zsh-defer -12p bindkey -M vicmd / vi-find-next-char
zsh-defer -12p bindkey -M vicmd '?' vi-find-prev-char
_zsh_autosuggest_strategy_atuin-quiet() {
  _zsh_autosuggest_strategy_atuin "$@" 2>/dev/null
}
# the default suggestion function errors quite noisily when it can't read the config. override it.
ZSH_AUTOSUGGEST_STRATEGY=(history completion atuin-quiet)

# https://direnv.net/
zsh-defer -12p -c "$(direnv hook zsh)"

# Needs to be initialized after all completions are loaded
export FPATH=~/.local/share/zsh/site-functions:$FPATH
if [ -n "$HOMEBREW_PREFIX" ]; then
	fpath[1,0]="$HOMEBREW_PREFIX/share/zsh/site-functions";
fi

autoload -Uz compinit
# having weird cache invalidation issues; for now, tell zsh to unconditionally reuse the cache. at some point completion will be buggy and we can investigate further then.
compinit -C

# Uses compdef; needs to come after compinit
compdef '_arguments --' fzf  # takes no positional arguments

if exists zoxide; then
   source <(zoxide init zsh)
   cd() { z "$@" && status; }
   # NOTE: only shows a FZF menu if there's a space after the query filter
   compdef __zoxide_z_complete cd
fi

zle-zi() {
	# TODO: this erases the second line of the prompt for some reason, lol, lmao
	if zi; then zle reset-prompt; else zle redisplay; fi
}
zle -N zle-zi
bindkey '^[c' zle-zi
bindkey -a '^[c' zle-zi

functions -c fork_github fork-github
unfunction fork_github

zsh-defer -12p -c 'if exists jj; then source <(jj util completion zsh); fi'
zsh-defer -12p -c "$(atuin gen-completions --shell zsh)"
# add `-p` here if you see errors during delayed loading
zsh-defer -c RPS1=
